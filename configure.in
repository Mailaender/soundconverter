dnl -*- Mode: autoconf -*-
dnl Process this file with autoconf to produce a configure script.
AC_PREREQ([2.68])
AC_INIT([soundconverter], [2.1.4])
AC_CONFIG_SRCDIR([bin/soundconverter.py])
AM_INIT_AUTOMAKE

m4_define(required_python_abi, [2.5])
m4_define(required_python_version, [2.5])
m4_define(required_pygtk_version, [2.24])
m4_define(required_pygnome_version, [2.16])

dnl put the ACLOCAL flags in the makefile
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"

ALL_LINGUAS="ar ast bg br ca cs da de el en_AU en_CA en_GB eo es et eu fa fi fr_CA fr_FR fr gl he hi hu it ja ko lv ml ms nl pl pt_BR pt ru sk sl sr@latin sr@Latn sr sv te tr uk vi zh_CN zh_TW"

GETTEXT_PACKAGE="soundconverter"
AC_SUBST([GETTEXT_PACKAGE])
AC_DEFINE_UNQUOTED([GETTEXT_PACKAGE], "$GETTEXT_PACKAGE", [Gettext package])
AM_GLIB_GNU_GETTEXT
IT_PROG_INTLTOOL

AM_PATH_PYTHON(required_python_version)


dnl Check for correctly installed pygtk
AC_MSG_CHECKING(for pygtk required_pygtk_version installed)
prog="
import pygtk; pygtk.require('2.0')
import gobject
assert gobject.pygtk_version >= tuple(map(int, 'required_pygtk_version'.split('.')))
"
if $PYTHON -c "$prog" 1>&AS_MESSAGE_LOG_FD 2>&AS_MESSAGE_LOG_FD; then
    AC_MSG_RESULT(found)
else
    AC_MSG_RESULT(not found)
    AC_MSG_ERROR(required pygtk version not found)
fi

dnl Check for correctly installed pygnome
AC_MSG_CHECKING(for python-gnome required_pygnome_version installed)
prog="
import pygtk; pygtk.require('2.0')
import gnome
assert gnome.gnome_python_version >= tuple(map(int, 'required_pygnome_version'.split('.')))
"
if $PYTHON -c "$prog" 1>&AS_MESSAGE_LOG_FD 2>&AS_MESSAGE_LOG_FD; then
    AC_MSG_RESULT(found)
else
    AC_MSG_RESULT(not found)
    AC_MSG_ERROR(required python-gnome version not found)
fi

AC_CONFIG_FILES([Makefile
        po/Makefile.in
        soundconverter/Makefile
        bin/Makefile
        data/Makefile
        data/soundconverter.desktop.in
        doc/Makefile
        ])
AC_OUTPUT
